//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
				if reference4 == "mouth"
				{
				"\![embed,OnMunchSoundBite]\s[20]*NOM NOM NOM NOM NOM NOM NOM NOM NOM NOM*\w9\s[0]"
				}
				elseif reference4 == "head"
				{
				"\s[2]Hey! Quit buggin' me!"
				"\s[4]Don't touch my hair! I styled it just right for any occasion!"
				"\s[7]Hey! \s[3]Quit buggin' me!"
				"\s[7]AAA! \s[4]Don't do that again!"
				"\s[3]That's strange...\w8 What are you doing?"
				"\s[3]What? You're weird..."
				}
				elseif reference4 == "eyes"
				{
					if CurrentMode == "sleeping"
					{
						CurrentMode = "normal"
						"\s[2]Wha-\w8 Hey! \s[4]That's not a nice way to wake me up! I was having\s[8] such a wonderful dream about...\w8 %(foods)...\w8 or TV... or Marge... or beer... \s[4]I can't remember! *sigh*"
					}
					else
					{
				"\s[5]Hey!\s[2] Quit buggin' me!"
				"\s[5]Ow!\s[2] That hurts! Stop it!"
				"\s[5]Ouch!\s[2] Don't poke my eyes! It hurts!"
					}
				}
				elseif reference4 == ""; OnSakuraMenu
				else; MusicPlayer.OnMouseDoubleClick
		}
		else
		{
			OnKeroMenu
		}
	}
}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if stroke % 40 == 0
		{
			if reference3 == 0; OnMouseMove.Sakura
			else; OnMouseMove.Kero
		}
	}
}

OnMouseMove.Sakura
{
	continuousstroke++

	if reference4 == "head"
	{
		if CurrentMode == "sleeping"
		{
			if continuousstroke >= 120
			{
				if stroke >= 120
				{
					stroke = 0
					totalpets++
					currentpetstreak++
					"\0\s[22]Ooh, that's a lotta pets!"
					"\0\s[21]..."
					"\0\s[21]Aww, thanks!"
				}
			}
			elseif stroke >= 40
			{
			stroke = 0
			totalpets++
			currentpetstreak++
			"\s[21]Mmm... pets..."
			"\s[21]...nice..."
			"\s[22]\![embed,OnHeheSoundBite]Heh heh"
			}
		}
		else
		{
			if continuousstroke >= 120
			{
				if stroke >= 120
				{
					stroke = 0
					totalpets++
					currentpetstreak++
					"\0\s[1]Ooh, that's a lotta pets!"
					"\0\s[1]..."
					"\0\s[1]Aww, thanks!"
				}
			}
			elseif stroke >= 20
			{
				stroke = 0
				totalpets++
				currentpetstreak++
				"\s[8]Mmm... pets..."
				"\s[1]Thanks..."
				"\s[1]\![embed,OnHeheSoundBite]Heh heh"
			}
		}
	}
}

OnMouseMove.Kero
{
	if reference4 == "head"
	{
		//"\1\s[10]"
	}
}

OnMouseWheel
{
	continuousstroke++

	if reference4 == "head"
	{
		if CurrentMode == "sleeping"
		{
			if continuousstroke >= 120
			{
				if stroke >= 120
				{
					stroke = 0
					totalpets++
					currentpetstreak++
					"\0\s[22]Ooh, that's a lotta pets!"
					"\0\s[21]..."
					"\0\s[21]Aww, thanks!"
				}
			}
			elseif stroke >= 40
			{
			stroke = 0
			totalpets++
			currentpetstreak++
			"\s[21]Mmm... pets..."
			"\s[21]...nice..."
			"\s[22]\![embed,OnHeheSoundBite]Heh heh"
			}
		}
		else
		{
			if continuousstroke >= 120
			{
				if stroke >= 120
				{
					 stroke = 0
					totalpets++
					currentpetstreak++
					"\0\s[1]Ooh, that's a lotta pets!"
					"\0\s[1]..."
					"\0\s[1]Aww, thanks!"
				}
			}
			elseif stroke >= 20
			{
					stroke = 0
					totalpets++
					currentpetstreak++
					"\s[8]Mmm... pets..."
					"\s[1]Thanks..."
					"\s[1]\![embed,OnHeheSoundBite]Heh heh"
			}
		}
	}
	elseif reference4 == ""; OnSakuraMenu
	else; MusicPlayer.OnMouseWheel
}

OnMouseLeaveAll
{
    if continuousstroke > 40 && continuousstroke > pethighscore
    {
        pethighscore = continuousstroke
        "\C\n\n\s[1]\![embed,OnWoohooSoundBite]Woohoo, new petting high score! %(pethighscore)!\e"
    }
    --
    if currentpetstreak > toppetstreak
    {
        toppetstreak = currentpetstreak
        //"\C\n\n\s[1]Woohoo, new high score! (%(toppetstreak) pets!)\e"
    }
    --
    stroke = 0
    continuousstroke = 0
    currentpetstreak = 0
}
