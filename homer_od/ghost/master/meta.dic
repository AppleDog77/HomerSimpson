//—————————————————————————————— Installing something ——————————————————————————————
//https://ukagakadreamteam.github.io/ukadoc/manual/list_shiori_event.html#OnInstallBegin

OnInstallComplete
{
	"\0\s[1]Hi there!\s[0][Installed %(reference1) (%(reference0)).]"
	--
	//A little menu to open or change to the ghost that was installed immediately
	if "ghost" _in_ reference0
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,call]Call %(reference0)\__q  \![*]\__q[OnInstallComplete.Action,change]Change to %(reference0)\__q\n\n"
		--
		"\![*]\q[Thanks,blank]"
	}
}

OnInstallComplete.Action
{
	if reference0 == "change"
	{
		"\![change,ghost,lastinstalled]"
	}
	else //call
	{
		"\![call,ghost,lastinstalled]"
	}
}

OnInstallFailure
{
	_reason = reference0
	case reference0
	{
		when "unlha32"; _reason = "could not load the dll to decompress lzh files"
		when "extraction"; _reason = "failed to decompress the file"
		when "invalid type"; _reason = "the install.txt is incorrect"
		when "unsupported"; _reason = "the archive is not supported"
		when "password"; _reason = "incorrect password"
		when "artificial"; _reason = "canceled by user"
	}

	"\0\s[4][Could not complete installation: %(_reason).]"
}


//—————————————————————————————— Creating a .nar file ——————————————————————————————
//https://ukagakadreamteam.github.io/ukadoc/manual/list_shiori_event.html#OnNarCreating

OnNarCreated
{
	//Makes a clickable link that'll open the location of the file they just made
	_nar = "\_a[OnNarLocation,""%(reference1)""]%(reference0)\_a"

	if CurrentMode == "sleeping"
			 {
			"\0\s[21][Successfully created %(_nar).]"
			 }
	else
			 {
			"\0\s[1][Successfully created %(_nar).]"
			 }
}

OnNarLocation
{
	_path = SPLITPATH(reference0)[0] + SPLITPATH(reference0)[1]

	"\![open,file,""%(_path)""]"
}
//—————————————————————————————— Network Update ——————————————————————————————
//https://ukagakadreamteam.github.io/ukadoc/manual/list_shiori_event.html#OnUpdateBegin

OnUpdateBegin
{
	"\0[Checking for updates.]\w8"
}

//reference0 is the number of new files, starting from 0
OnUpdateReady
{
	reference0++ //Number of files starts from 0, so this displays the correct amount
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "is"
	if reference0 != 1; {_s = "s"; _are = "are"}
	
	"\0[There %(_are) %(reference0) new file%(_s).]\w8"
}

//When the update finishes. Don't forget to initialize any new variables you've created! I highly highly recommend using OnInitialize for this (it's in boot.dic), in case the user updates via the ghost explorer or some other means.
//reference0 is 'none' if there were no new files to update with
OnUpdateComplete
{
	if reference0 == "none"
	{
		 if CurrentMode == "sleeping"
			 {
			"\0\s[22][There are no new files.]"
			 }
			 else
			 {
			"\0\s[4][There are no new files.]"
			 }
	}
	else
	{
		 if CurrentMode == "sleeping"
			 {
			"\0\s[22][Update complete.]"
			 }
			 else
			 {
			"\0\s[1][Update complete.]"
			 }
	}
}

OnUpdateFailure
{	
	_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //For MD5 errors, this gets the name and extension of the offending file
	_reason = reference0
	case reference0
	{
		when "timeout"; _reason = "connection timed out"
		when "md5 miss"; _reason = "MD5 error on file %(_file)\n\nPlease contact the ghost author for assistance"
		when "artificial"; _reason = "canceled by user"
	}
	"\0\s[4][Could not update: %(_reason).]"
}

//—————————————————————————————— SNTP (clock fixing) ——————————————————————————————
//https://ukagakadreamteam.github.io/ukadoc/manual/list_shiori_event.html#OnSNTPBegin

OnSNTPCompare
{
	if reference3 != 0
	{
		_s = ""; if reference3 != 1; _s = "s"

		"\0[The clock is off by %(reference3) second%(_s).]\n\n"
		--
		"\![*]\q[Fix it,script:\6]  \![*]\q[Leave it,blank]"
	}
	else
	{
		"\0[The clock is accurate.]"
	}
}

OnSNTPCorrect
{
	"\0[The clock has been corrected.]"
}

OnSNTPFailure
{
	"\0\s[4]\![embed,OnDohSoundBite]D'oh! [Could not correct the clock.]"
}

//Other Stuff, gotten from GT Ghost Template by Zarla
 
OnScreenSaverEnd
 {
	 if CurrentMode == "sleeping"
	 {
		 CurrentMode = "normal"
		"\0\s[4]Aww, I missed the screensaver! \s[3]I guess I'll wake up now, \s[4]so I don't miss the next one...\e"
	 }
	 else
	 {
	"\0\s[4]Aww, I wanted to see what happens next!\e"
	 }
 }

OnShellScaling
{
	if reference0 >= 150 //this number is the percentage that you're scaling the ghost up to. In this case, it's 150%.
	{
			 if CurrentMode == "sleeping"
			 {
			"\0\s[22]\![embed,OnLaughSoundBite]Hahaha! All...\w8 Hail...\w8 King...\w8 Homer...\e"
			 }
			 else
			 {
			"\0\s[1]\![embed,OnLaughSoundBite]Hahaha! All Hail King Homer!\e"
			 }
	}
	elseif reference0 == 100 //this is 100%
	{
			 if CurrentMode == "sleeping"
			 {
			"\0\s[23]Aw... I'm back to normal...\e"
			 }
			 else
			 {
			"\0\s[3]Aw, I'm back to normal!\e"
			 }
	}
	elseif reference0 <= 50 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs. Odie: I made it 50% and lower.
	{
			if CurrentMode == "sleeping"
			 {
			"\0\s[21]...That %(yummy) %(foods) made me smaller...\w8 I wonder if I can go through that strange door now...\e"
			 }
			 else
			 {
			"\0\s[0]Ooh, I wonder if I can now get those pie pieces that fell into the mouse hole a while back...\e"
			 }
	}
}

OnVanishSelecting
{
	"\0\s[3]What's going on? ... \w8\s[7]*gasp* Did you get fired and have to leave?"
}

OnVanishSelected
{
	"\0\s[4]Oh, you got fired? Aw, ok, guess I won't be seeing you around anymore...\w8 Bye...\w8\w8\w8 Hope we can meet again someday...\w9\w9\_w[5000]\e"
}

OnVanishCancel
{
	"\0\s[1]Woohoo! You didn't get fired after all! Maybe you could %(ideas)! \s[1]Let's celebrate!"
}
