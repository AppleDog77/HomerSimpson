KeroMenuGreetings : nonoverlap
{
	if CurrentMode == "sleeping"
	{
	"\0\s[5]Mmm...\w8 %(foods)..."
	"\0\s[21]Mmm...\w8 %(foods)..."
	"\0\s[22]Mmm...\w8 %(foods)..."
	}
	else
	{
	"\0\s[8]Mmm...\w8 %(foods)..."
	"\0\s[3]\![embed,OnBurpSoundBite]*BURP*\![no-autopause]"
	}
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnKeroMenu : all
{
	"\1\s[10]\0\b2\![set,autoscroll,disable]"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; KeroMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer
	"\n\n\_q"
	
	//Put anything you want here, anything at all. For example, here's how you can add your own menu... You can make as many like this as you want! Uncomment if you want to test it out, or erase if you don't want it
	"\![*]\q[Minigames,OnGamesMenu]\n\n"
	
	"\![*]\q[Close Menu,OnRestoreDonut]"
}

OnGamesMenu : all
{
	CurrentMode = "normal"
	"\1\s[11]\0\s[1]Ooh, minigames! \s[0]Which one do you wanna play?"
	"\n\n\_q"
	
	"\![*]\q[Word Search,OnStartWordSearch]\n"
	"\![*]\q[Word Jumble,OnWordJumble.Start]\n"
	"\![*]\q[Hangman,OnStartHangman]\n"
	"\![*]\q[Back to main menu,OnKeroMenu]\n"
	"\![*]\q[Nevermind,OnRestoreDonut]"
}

//Written by Zichqec https://ukagaka.zichqec.com/index.html

//If this all looks scary to you, no worries! All you need to do is set a few options in OnStartWordSearch, set up dialogue in WordSearchDialogue, and then add your own pool of words for it to choose from in WordSearchWords. If it gives you any trouble, let me know!

//Use this to start a new word search game: "\![*]\q[Word Search,OnStartWordSearch]"
OnStartWordSearch
{
	_rightadjust = 0 //Don't touch these ones
	_highlightcolor = ""
	
	//---Options. Feel free to set these however you like.---
	
	//_defaultballoon: Set this to the name of your balloon, as seen in the right click menu. This lets you adjust the color and positioning specifically to your balloon, without breaking it completely for other balloons!
	//_debug: Hides all letters that are not a part of words, so you can test and see how well your words are being distributed
	//_gridsize: The dimensions of your word search. I created this based on a 10x10 grid, but you can play around with it if you like.
	//_fontsize: Font size of the grid. Adjust to your liking. The bigger the better, this game can be hard on the eyes.
	//_maxwords: How many words it will attempt to add to the grid. Note: Sometimes adding a word fails, so you may end up with less words!
	//_attempts_to_place: How many times it will attempt to add a single word to the grid. If you put this higher you'll probably end up with the max amount of words in the puzzle more often, but it may impact performance.
	//_rightadjust: You can use this to center the word search in your balloon (this is in pixels). Since people can use whatever balloon they want, though, I put a check to check if they're in your default balloon. Make sure to put your balloon's name there. Note: Only works in YAYA, you might need a different setup using OnBalloonChange if you're using AYA.
	//_backwardsallowed:  1 if you want words to be able to appear backwards, 0 if you don't.
	//_highlightinverted: Words that are found are highlighted using your balloon's anchor color. If you would prefer for everything to be the anchor color, and for found words to be the color of menu choices, you can set that here.
	//_highlightcolor: Color you want for highlighting, as an R,G,B value. If you want the default color used by your balloon, put "default.anchor" instead
	
	//Note: If you want to change the anchor color that's used in the grid, you can do so by adding a \f[anchorcolor,(R),(G),(B)] tag in the part of the Word Search code that displays the grid. You can also use similar tags to change the color of \__q tags, I believe. Ukadoc will have more info.
	
	_defaultballoon = "Sky Balloon"
	_debug = 0
	_gridsize = 10
	_fontsize = 17
	_maxwords = 10
	_attempts_to_place = 15
	if SHIORI3FW.BalloonName == _defaultballoon; _rightadjust = 20
	_backwardsallowed = 0
	_highlightinverted = 0
	if SHIORI3FW.BalloonName == _defaultballoon; _highlightcolor = "100,0,160"
	else; _highlightcolor = "default.anchor"
	
	//Don't touch these
	WSOpts = (_fontsize,_rightadjust,_highlightinverted,_highlightcolor)
	CreateWordSearch(_debug,_gridsize,_maxwords,_attempts_to_place,_backwardsallowed)
	WordsLeft = WordList
	OnWordSearch
}



//All your dialogue goes here. Don't add \e to the end of any of these.
WordSearchDialogue
{
	if _argv[0] == "Menu During Play" //Menu option at the bottom while the game is being played, such as a button to give up/quit
	{
		"\n\![*]\q[Give up,OnWordSearchQuit]\e"
	}
	elseif _argv[0] == "Invalid Word" //If the user puts in a word that isn't on the word list or was already guessed - _argv[1] is the word the user guessed
	{
		"\s[3]Hmm, I don't know that word. %(_argv[1]). Did you make it up just now or something? Or maybe you already found that word..."
	}
	elseif _argv[0] == "Invalid Move" //If the user tried an illegal diagonal move
	{
		"\s[3]I don't think that's how the game works. If I remember correctly, what Lisa said was that moves have to be straight lines;\w4 up,\w4 down,\w4 left,\w4 right,\w4 or diagonal."
	}
	elseif _argv[0] == "Found Word" //If the user found a word - _argv[1] is the word that was found
	{
		"\s[0]Ooh, you found the word %(_argv[1])."
	}
	elseif _argv[0] == "Finished Puzzle" //If the user completed the puzzle - _argv[1] is the last word the user found. Might be a good idea to add menu buttons here.
	{
		"\1\s[10]\0\s[0]%(_argv[1]) was the last word!\w5 You found all the words! \w5\s[1]\![embed,OnWoohooSoundBite]Woohoo!"
		--
		"\_q\n\![*]\q[Play again,OnStartWordSearch]  \![*]\q[Play something else,OnGamesMenu]\n\![*]\q[I'm done,OnRestoreDonut]\e"
		
		ERASEVAR("WordSearch")
		ERASEVAR("WordList")
		ERASEVAR("WordsLeft")
		ERASEVAR("SearchHighlight")
		ERASEVAR("WSOpts")
	}
}
OnWordSearchQuit
{
	"\1\s[10]\0\s[0]Good idea! \s[3]That puzzle looked pretty tough..."
}
//Words you want to be placed in the puzzle go here. Be mindful of how long your words are! Make sure they're not too long to fit the grid. Probably don't make them too short, either. I'd recommend at least 4 letters. And have plenty of words available, the more the better! This has to be an array, so if you're using my other games, you won't be able to share a word pool between them. (Unless you get clever and have those word pools read from here using something like ANY(WordSearchWords)
WordSearchWords : array
{
	"Brownie"
	"Cake"
	"Cupcake"
	"Donut"
	"Chocolate"
	"Candy"
	"Cookie"
	"Cream"
	"Beer"
	"Chicken"
	"Porkchop"
	"Lollipop"
	"Pepper"
	"Honey"
	"Sandwich"
	"Simpsons"
	"Homer"
	"Marge"
	"Lisa"
	"Bart"
	"Maggie"
	"Duff"
	"Abraham"
	"Mona"
	"Yummy"
	"Delicious"
	"Lobster"
	"Pinchy"
	"Crayon"
	"Plopper"
	"Tulip"
	"Peanut"
	"Kavner"
	"Barney"
	"Quimby"
	"Yeardley"
	"Waylon"
	"Smithers"
	"Burns"
	"Flanders"
	"Seymour"
	"Skinner"
	"Milhouse"
}

OnWordSearch
{
	_highlightarray = SearchHighlight
	_endRow = ""
	_endCol = ""
	_startRow = ""
	_startCol = ""
	_RowDist = ""
	_ColDist = ""
	
	//Sets up some variables for the X and Y values, and also sets up variables to check that the user doesn't make an illegal diagonal move
	if reference0 == "StartLetter"
	{
		_startRow = TOINT(reference1)
		_startCol = TOINT(reference2)
	}
	elseif reference0 == "EndLetter"
	{
		_endRow = TOINT(reference1)
		_endCol = TOINT(reference2)
		_startRow = TOINT(reference3)
		_startCol = TOINT(reference4)
		
		//These bits change the numbers to not be negatives, so they can be compared properly
		_RowDist = _startRow - _endRow
		if _RowDist < 0; _RowDist = -_RowDist
		
		_ColDist = _startCol - _endCol
		if _ColDist < 0; _ColDist = -_ColDist
	}
	
	_valid = 0
	_direction = ""
	_guessedword = ""
	_start = ""
	_end = ""
	//If the user attempted a guess
	if reference0 == "EndLetter"
	{
		if _startRow == _endRow //If this is a valid move (Horizontal)
		{
			_valid = 1
			if _startCol > _endCol; {_direction = "Left"; _start = _endCol; _end = _startCol}
			else; {_direction = "Right"; _start = _startCol; _end = _endCol}
			
			for _i = _start; _i <= _end; _i++
			{
				_guessedword += WordSearch[_startRow][_i]
			}
			
			if _direction == "Left"
			{
				_guessedword = WSReverse(_guessedword)
			}
			
			//Highlighting
			for _i = 0; _i < STRLEN(_guessedword); _i++
			{
				_row = _highlightarray[_startRow]
				_row[_start + _i] = "1" 
				_highlightarray[_startRow] = _row
			}
		}
		elseif _startCol == _endCol //Vertical
		{
			_valid = 1
			if _startRow > _endRow; {_direction = "Up"; _start = _endRow; _end = _startRow}
			else; {_direction = "Down"; _start = _startRow; _end = _endRow}
			
			for _i = _start; _i <= _end; _i++
			{
				_guessedword += WordSearch[_i][_startCol]
			}
			
			if _direction == "Up"
			{
				_guessedword = WSReverse(_guessedword)
			}
			
			//Highlighting
			for _i = 0; _i < STRLEN(_guessedword); _i++
			{
				_row = _highlightarray[_start + _i]
				_row[_startCol] = "1" 
				_highlightarray[_start + _i] = _row
			}
		}
		elseif _RowDist == _ColDist //Diagonal, if it's valid
		{
			_valid = 1
			
			if _startCol > _endCol && _startRow > _endRow //Going left and up
			{
				_direction = "Left Up"
				_ii = _startCol
				for _i = _startRow; _i >= _endRow; _i--
				{
					_guessedword += WordSearch[_i][_ii]
					
					//Highlighting
					_row = _highlightarray[_i]
					_row[_ii] = "1"
					_highlightarray[_i] = _row
					
					_ii--
				}
			}
			elseif _startCol > _endCol && _startRow < _endRow //going left and down
			{
				_direction = "Left Down"
				
				_ii = _startCol
				for _i = _startRow; _i <= _endRow; _i++
				{
					_guessedword += WordSearch[_i][_ii]
					
					//Highlighting
					_row = _highlightarray[_i]
					_row[_ii] = "1"
					_highlightarray[_i] = _row
					
					_ii--
				}
			}
			elseif _startCol < _endCol && _startRow > _endRow //going right and up
			{
				_direction = "Right Up"
				_ii = _startRow
				for _i = _startCol; _i <= _endCol; _i++
				{
					_guessedword += WordSearch[_ii][_i]
					
					//Highlighting
					_row = _highlightarray[_ii]
					_row[_i] = "1"
					_highlightarray[_ii] = _row
					
					_ii--
				}
			}
			else //going right and down
			{
				_direction = "Right Down"
				
				_ii = _startCol
				for _i = _startRow; _i <= _endRow; _i++
				{
					_guessedword += WordSearch[_i][_ii]
					
					//Highlighting
					_row = _highlightarray[_i]
					_row[_ii] = "1"
					_highlightarray[_i] = _row
					
					_ii++
				}
			}
		}
		else //If it's an invalid move
		{
			_valid = 0
		}
		
		
	}
	
	//If the word is in the list of words to find, mark it, and also remove the word from the list of words that still need to be found
	_find = ASEARCH(_guessedword,WordsLeft)
	if _find != -1
	{
		_valid = 2
		WordsLeft[_find] = IARRAY
	}
	
	if _valid == 2; SearchHighlight = _highlightarray
	else; _highlightarray = SearchHighlight
	
	//Set up the display of the grid
	_fheight = 10
	if WSOpts[0] > 0; _fheight = WSOpts[0]
	
	_rightadjust = 0
	if WSOpts[1] > 0; _rightadjust = WSOpts[1]
	
	_inverthighlight = 0
	if WSOpts[2] == 1; _inverthighlight = WSOpts[2]
	
	
	_display = "\f[anchorfontcolor,%(WSOpts[3])]\f[height,%(_fheight)]\f[bold,1]"
	if reference0 == "Win"; _display += "\f[anchorstyle,none]\f[cursorstyle,none]"
	for _i = 0; _i < ARRAYSIZE(WordSearch); _i++
	{
		_display += "\_l[%(_rightadjust)]"
		_row = WordSearch[_i]
		for _ii = 0; _ii < ARRAYSIZE(_row); _ii++
		{
			
			_col = _row[_ii]
			if reference0 == "Win" //If the user already won, don't display with clickable buttons
			{
				
				
				_tag1 = ""
				_tag2 = ""
				if _highlightarray[_i][_ii] == "1";
				{
					if _inverthighlight == 1
					{
						_tag1 = "\__q[OnBlank]"
						_tag2 = "\__q"
					}
					else
					{
						_tag1 = "\_a[OnBlank]"
						_tag2 = "\_a"
					}
				}
				elseif _inverthighlight == 1 //if this is a random letter but inverted highlighting is on
				{
					_tag1 = "\_a[OnBlank]"
					_tag2 = "\_a"
				}
				_display += "%(_tag1)%(_col)%(_tag2) "
			}
			else
			{
				//IF the user has selected a letter, give it a unique link and highlight it
				if reference0 == "StartLetter" && _i == _startRow && _ii == _startCol
				{
					_tag = "\_a"; if _inverthighlight == 1; _tag = "\__q"
					_display += "%(_tag)[OnWordSearch,CancelStart,%(_i),%(_ii),%(_startRow),%(_startCol)]%(_col)%(_tag) "
				}
				else //Normal display with clickable buttons
				{
					_ref = ""
					if reference0 == "StartLetter"; _ref = "EndLetter"
					else; _ref = "StartLetter"
					_tag = ""
					if _inverthighlight == 1
					{
						_tag = "\_a"
						if _highlightarray[_i][_ii] == "1"; _tag = "\__q"
					}
					else
					{
						_tag = "\__q"
						if _highlightarray[_i][_ii] == "1"; _tag = "\_a"
					}
					
					_display += "%(_tag)[OnWordSearch,%(_ref),%(_i),%(_ii),%(_startRow),%(_startCol)]%(_col)%(_tag) "
					
				}
			}
		}
		_display += "\n"
	}
	//Display word list
	_display += "\n[half]\f[height,default]\f[cursorstyle,default]"
	foreach WordList; _word
	{
		_exist = ASEARCH(_word,WordsLeft)
		if _exist == -1; _display += "\f[strike,1]" //Strike it out if it's been found already
		_display += "%(_word)"
		if _exist == -1; _display += "\f[strike,0]"
		_display += "  "
	}
	if ARRAYSIZE(WordsLeft) == 0 && reference0 != "Win"; "\![raise,OnWordSearch,Win,%(_guessedword)]\e" //If the user won, restart this function so that it can remake the board
	--
	//Dialogues for when you've made a turn. I ended up making a separate function for this because wow this function got long
	if reference0 == "EndLetter"
	{
		_display += "\n\_q\f[default]"
		if _valid == 2 //If the user found a word
		{
			_display += "%(WordSearchDialogue('Found Word',_guessedword))"
		}
		elseif _valid == 1 //If the user found a word, but it's not in the word list or has already been guessed
		{
			_display += "%(WordSearchDialogue('Invalid Word',_guessedword))"
		}
		else //If the user made an invalid move
		{
			_display += "%(WordSearchDialogue('Invalid Move'))"
		}
	}
	elseif reference0 == "Win" //If the user won
	{
		_display += "\n\_q\f[default]%(WordSearchDialogue('Finished Puzzle',reference1))"
	}
	if reference0 != "Win"; _display += "\n\f[default]%(WordSearchDialogue('Menu During Play'))"
	"\b2\f[name,Courier New]\![set,autoscroll,disable]\_q%(_display)\*\e" //Actual display
}

WSReverse //Reverses the string it's given, for the purposes of placing some words backwards
{
	_output = ""
	for _i = 0; _i < STRLEN(_argv[0]); _i++
	{
		_output = SUBSTR(_argv[0],_i,1) + _output
	}
	_output
}

CreateWordSearch //:deargodhelpme:
{
	_dimensions = 10 //Width and height of the grid
	if _argv[1] > 0; _dimensions = _argv[1]
	
	_totalwords = 10 //How many words to attempt to add, total (each word will be tried multiple times if it does not fit)
	if _argv[2] > 0; _totalwords = _argv[2]
	
	_wordtries = 15 //How many times it will attempt to place a word before giving up
	if _argv[3] > 0; _wordtries = _argv[3]
	
	_allowreverse = 1
	if _argv[4] == 0; _allowreverse = 0
	
	WordList = IARRAY
	//Create a grid of .s
	WordSearch = IARRAY
	
	//Setting up the base grid, and the array for highlighting words
	SearchHighlight = IARRAY
	for _i = 0; _i < _dimensions; _i++ //Rows
	{
		_highlight = ""
		_columns = ""
		for _ii = 0; _ii < _dimensions; _ii++ //Columns
		{
			if _ii != 0; {_columns += ","; _highlight += ","}
			_columns += "."
			_highlight += "0"
		}
		WordSearch ,= _columns
		SearchHighlight ,= _highlight
	}
	
	//Get words and add them to the grid
	_availwords = WordSearchWords
	_toadd = ""
	_orig = ""
	_placed = 0
	for _i = 0; _i < _totalwords; _i++
	{
		//Check to make sure there's a word to add. If there is, pick a random one, then erase it from the array. If the word is too long, skip it and start again.
		if ARRAYSIZE(_availwords) == 0; {LOGGING("Ran out of words to add");break}
		_toadd = TOUPPER(ANY(_availwords))
		LOGGING("Entered loop 1 - Start new word (%(_toadd))")
		_availwords[LSO] = IARRAY
		if STRLEN(_toadd) > _dimensions; {LOGGING("%(_toadd) too long for current grid size");continue}
		
		//This loop attempts to place the word several times, depending on how you have it set
		_placed = 0
		_orig = _toadd
		for _ii = 0; _ii < _wordtries && _placed == 0; _ii++
		{
			//Sets an orientation, decides if it should be reversed
			_orient = ANY("Horizontal,Vertical,Diagonal \,Diagonal /")
			if _allowreverse == 1 && RAND(2) == 1
			{
				_toadd = WSReverse(_toadd)
			}
			_len = STRLEN(_toadd)
			_x = RAND(_dimensions)
			_y = RAND(_dimensions)
			LOGGING("Entered loop 2 - Try to find a place for new word (%(_x),%(_y) orient: %(_orient), len: %(_len)")
			
			//Each of these attempts to place it based on the orientation
			if _orient == "Horizontal" //---Left to Right---
			{
				if (_x + _len) > _dimensions //If it's too long
				{
					LOGGING("Couldn't place %(_toadd), X: %(_x), orient: %(_orient), len: %(_len), tries so far: %(_ii)")
					continue
				}
				else //If it has enough space
				{
					_skip = 0
					_rowtochange = WordSearch[_y]
					for _iii = 0; _iii < STRLEN(_toadd); _iii++ //Check to see if any other words are in the way
					{
						LOGGING("Entered loop 3 - Attempt to add new word, see if other words block it")
						if _rowtochange[_x + _iii] == "." || _rowtochange[_x + _iii] == SUBSTR(_toadd,_iii,1) //If it's an empty space or a space that matches the current letter
						{
							_rowtochange[_x + _iii] = SUBSTR(_toadd,_iii,1)
							
						}
						else //If it's a taken space
						{
							_skip = 1
							LOGGING("Can't place %(_toadd), %(_rowtochange[_x + _iii]) in the way.")
							break
						}
					}
					
					if _skip == 1 //If it got blocked, skip and try placing in another place
					{
						_skip = 0
						continue
					}
					else //If it wasn't blocked, go ahead and add it
					{
						WordSearch[_y] = _rowtochange
						_placed = 1
					}
				}
			}
			elseif _orient == "Vertical" //---Top to Bottom---
			{
				if (_y + _len) > _dimensions //If it's too long
				{
					LOGGING("Couldn't place %(_toadd), Y: %(_y), orient: %(_orient), len: %(_len), tries so far: %(_ii)")
					continue
				}
				else //If it has enough space
				{
					_skip = 0
					//Make a temp array of this column
					_coltochange = IARRAY
					for _iii = 0; _iii < ARRAYSIZE(WordSearch[_x]); _iii++
					{
						_coltochange ,= WordSearch[_iii][_x]
					}
					
					for _iii = 0; _iii < STRLEN(_toadd); _iii++ //Check to see if any other words are in the way
					{
						LOGGING("Entered loop 3 - Attempt to add new word, see if other words block it")
						if _coltochange[_y + _iii] == "." || _coltochange[_y + _iii] == SUBSTR(_toadd,_iii,1) //If it's an empty space or a space that matches the current letter
						{
							LOGGING("placed %(SUBSTR(_toadd,_iii,1))")
							_coltochange[_y + _iii] = SUBSTR(_toadd,_iii,1)
							
						}
						else //If it's a taken space
						{
							_skip = 1
							LOGGING("Can't place %(_toadd), %(_coltochange[_y + _iii]) in the way.")
							break
						}
					}
					
					if _skip == 1 //If it got blocked, skip and try placing in another place
					{
						_skip = 0
						continue
					}
					else //If it wasn't blocked, go ahead and add it
					{
						//_x should be the column that needs changed... have to extract it out of every row though and then put it back.
						for _iii = 0; _iii < ARRAYSIZE(_coltochange); _iii++
						{
							_row = WordSearch[_iii]
							_row[_x] = _coltochange[_iii]
							WordSearch[_iii] = _row
						}
						//WordSearch[_x] = _coltochange
						_placed = 1
					}
				}
			}
			elseif _orient == "Diagonal \" //Diagonal top left to bottom right
			{
				if (_y + _len) > _dimensions || (_x + _len) > _dimensions //If it's too long
				{
					LOGGING("Couldn't place %(_toadd), Y: %(_y), orient: %(_orient), len: %(_len), tries so far: %(_ii)")
					continue
				}
				else //If it has enough space
				{
					_skip = 0
					//Make a temp array of this column
					_diagtochange = IARRAY
					for _iii = 0; _iii < _len; _iii++
					{
						_diagtochange ,= WordSearch[_y + _iii][_x + _iii]
					}
					
					for _iii = 0; _iii < STRLEN(_toadd); _iii++ //Check to see if any other words are in the way
					{
						LOGGING("Entered loop 3 - Attempt to add new word, see if other words block it")
						if _diagtochange[_iii] == "." || _diagtochange[_iii] == SUBSTR(_toadd,_iii,1) //If it's an empty space or a space that matches the current letter
						{
							LOGGING("placed %(SUBSTR(_toadd,_iii,1))")
							_diagtochange[_iii] = SUBSTR(_toadd,_iii,1)
							
						}
						else //If it's a taken space
						{
							_skip = 1
							LOGGING("Can't place %(_toadd), %(_diagtochange[_iii]) in the way.")
							break
						}
					}
					
					if _skip == 1 //If it got blocked, skip and try placing in another place
					{
						_skip = 0
						continue
					}
					else //If it wasn't blocked, go ahead and add it
					{
						//_x should be the column that needs changed... have to extract it out of every row though and then put it back.
						for _iii = 0; _iii < ARRAYSIZE(_diagtochange); _iii++
						{
							_row = WordSearch[_iii + _y]
							_row[_x + _iii] = _diagtochange[_iii]
							WordSearch[_iii + _y] = _row
						}
						_placed = 1
					}
				}
			}
			elseif _orient == "Diagonal /" //diagonal bottom left to top right
			{
				if (_y - _len) < 0 || (_x + _len) > _dimensions //If it's too long
				{
					LOGGING("Couldn't place %(_toadd), Y: %(_y), orient: %(_orient), len: %(_len), tries so far: %(_ii)")
					continue
				}
				else //If it has enough space
				{
					_skip = 0
					//Make a temp array of this column
					_diagtochange = IARRAY
					for _iii = 0; _iii < _len; _iii++
					{
						LOGGING("Diagpath %(_iii): '%(WordSearch[_y - _iii][_x + _iii])'")
						_diagtochange ,= WordSearch[_y - _iii][_x + _iii]
					}
					
					for _iii = 0; _iii < STRLEN(_toadd); _iii++ //Check to see if any other words are in the way
					{
						LOGGING("Entered loop 3 - Attempt to add new word, see if other words block it")
						if _diagtochange[_iii] == "." || _diagtochange[_iii] == SUBSTR(_toadd,_iii,1) //If it's an empty space or a space that matches the current letter
						{
							LOGGING("placed %(SUBSTR(_toadd,_iii,1))")
							_diagtochange[_iii] = SUBSTR(_toadd,_iii,1)
							
						}
						else //If it's a taken space
						{
							_skip = 1
							LOGGING("Can't place %(_toadd), %(_diagtochange[_iii]) in the way.")
							break
						}
					}
					
					if _skip == 1 //If it got blocked, skip and try placing in another place
					{
						_skip = 0
						continue
					}
					else //If it wasn't blocked, go ahead and add it
					{
						//_x should be the column that needs changed... have to extract it out of every row though and then put it back.
						//_c = 0
						LOGGING("To place: %(_diagtochange)")
						for _iii = 0; _iii < ARRAYSIZE(_diagtochange); _iii++
						{
							_row = WordSearch[_y - _iii]
							_row[_x + _iii] = _diagtochange[_iii]
							WordSearch[_y - _iii] = _row
							//_c++
						}
						//WordSearch[_x] = _diagtochange
						_placed = 1
					}
				}
			}
		}
		if _placed == 1; WordList ,= _orig
	}
	
	//Fill empty spaces on the grid
	for _i = 0; _i < ARRAYSIZE(WordSearch); _i++
	{
		_row = WordSearch[_i]
		for _ii = 0; _ii < ARRAYSIZE(_row); _ii++
		{
			//Comment this out if you want just .s so you can see the words it placed
			if _row[_ii] == "." && _argv[0] != 1; _row[_ii] = ANY("A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z")
		}
		WordSearch[_i] = _row
	}
}
//Written by Zichqec https://ukagaka.zichqec.com/
//v1.0.1
/*
This is an improved version of my original Junior Jumble game. In this version of the game, the user is prompted with a category for the words they will be solving, because as it turns out unscrambling words is hard when you have no idea what they might be!

By default, the user is asked to solve 5 words before they win. You can adjust this at the top of the OnWordJumble.Start function.

To start the game, call the event OnWordJumble.Start in a menu option/raise tag/etc. Example: \![*]\q[Play Word Jumble,OnWordJumble.Start]
*/


//——————————————— Dialogue ———————————————
//I set these up to display the final words at the end. If you'd rather have your ghost say something about the user winning, you can replace it with simple dialogue or what have you.
/*
Quick reference for the local variables in the following functions, in case you want to change things up.
In particular, you might be interested in _word and _scrambled, but the rest you can probably ignore.

_guess - only available if the user loses. Their incorrect guess, with sakurascript tags escaped
_category_name - the name of the current category
_word_list - a list of all the words yet to be guessed, delimited by C_BYTE1
_word - the current word that is being guessed
_correct_guesses - words that have been guessed correctly so far, delimited by WordJumble.Delimiter (set to "||" by default)
_scrambled - the scrambled version of the word to be guessed
*/

OnWordJumble.Win : all
{
	_category_name = reference2
	_word_list = reference3
	_word = SPLIT(_word_list,C_BYTE1)[0]
	_correct_guesses = reference4
	_scrambled = reference5
	
	"\1\s[10]\0\b[2]\![set,autoscroll,disable]\_q"
	"\s[1]\![embed,OnWoohooSoundBite]Woohoo! You win!"
	"\nCategory:\_w[] %(_category_name)"
	"\n\n"
	"\![*]\__q[OnWordJumble.Start]Play again\__q  \![*]\__q[OnRestoreDonut]Done\__q"
	"\n\n"
	
	WordJumble.MakeDisplay(_correct_guesses)
}

OnWordJumble.Lose : all
{
	_guess = SHIORI3FW.EscapeAllTags(reference0)
	_category_name = reference2
	_word_list = reference3
	_word = SPLIT(_word_list,C_BYTE1)[0]
	_correct_guesses = reference4
	_scrambled = reference5
	
	"\1\s[10]\0\b[2]\![set,autoscroll,disable]\_q"
	"\s[3]Game over! The correct word was %(_word). I guess you could try again, but..."
	"\nCategory: %(_category_name)"
	"\n\n"
	"\![*]\__q[OnWordJumble.Start]Play again\__q  \![*]\__q[OnRestoreDonut]Done\__q"
	"\n\n"
	
	if _correct_guesses != ""; WordJumble.MakeDisplay(_correct_guesses)
	
	"\f[color,disable]%(_scrambled)\f[color,default]\n"
	"\f[strike,1]%(_guess)\f[strike,default]\n\n"
}

OnWordJumble.Quit : all
{
	_category_name = reference2
	_word_list = reference3
	_word = SPLIT(_word_list,C_BYTE1)[0]
	_correct_guesses = reference4
	_scrambled = reference5
	
	"\0\b[2]\![set,autoscroll,disable]\_q"
	"\![close,inputbox,OnWordJumble.Guess]"
	"\1\s[10]\0\s[0]Good idea! \s[3]I was gettin' sorta bored anyway."
	"\nCategory: %(_category_name)"
	"\n\n"
	"\![*]\__q[OnWordJumble.Start]Play again\__q  \![*]\__q[OnRestoreDonut]Done\__q"
	"\n\n"
	
	if _correct_guesses != ""; WordJumble.MakeDisplay(_correct_guesses)
	
	"\f[color,disable]%(_scrambled)\f[color,default]\n"
	"\f[strike,1]%(_word)\f[strike,default]\n\n"
}


//——————————————— Categories / Words ———————————————
//You can write as many categories as you want. Just copy one of these, and pick a new name.
//The name of the function after the "WordJumble.Category." part doesn't matter, but for simplicity's sake, I'd make it the name of the category (minus any special characters/spaces)
//The first string in each array should be the name of the category.

//The more categories and words in each category you have, the better! But mind that anagrams can be mean. Also, be mindful of things that can scramble into profanity, etc.

WordJumble.Category.SweetsFoods : array
{
	"Sweets and Foods"
	
	"Brownie"
	"Cupcake"
	"Donut"
	"Chocolate"
	"Cookie"
	"Chicken"
	"Porkchop"
	"Lobster"
	"Peanut"
	"Honey"
	"Sandwich"
	"Pepper"
}

WordJumble.Category.Simpsons : array
{
	"Simpsons"
	
	"Homer"
	"Marge"
	"Lisa"
	"Bart"
	"Maggie"
	"Abraham"
	"Mona"
}

WordJumble.Category.Names : array
{
	"Simpsons-related Names"

	"Pinchy"
	"Plopper"
	"Kavner"
	"Barney"
	"Quimby"
	"Yeardley"
	"Springfield"
	"Waylon"
	"Smithers"
	"Burns"
	"Flanders"
	"Seymour"
	"Skinner"
	"Milhouse"
}


//——————————————— Game logic ———————————————
OnWordJumble.Start
{
	//The maximum number of words that you want the user to have to guess before they win.
	//Note that if there are not this many words in the category, it will simply use all the available words once.
	_max_words = 5
	
	
	//Picking a category
	_categories = GETFUNCLIST("WordJumble.Category.")
	_category = EVAL("%(ANY(_categories))")
	_category_name = _category[0]
	_category[0] = IARRAY
	
	_word_list = ""
	for _i = 0; _i < _max_words && ARRAYSIZE(_category) > 0; _i++
	{
		if _i != 0; _word_list += C_BYTE1
		_word_list += ANY(_category)
		_category[LSO] = IARRAY
	}
	
	"\![close,inputbox,OnWordJumble.Guess]\1\s[11]\0\![raise,OnWordJumble.Display,start,,""%(_category_name)"",""%(_word_list)""]"
}

//Main game window
OnWordJumble.Display : all
{
	_category_name = reference2
	_word_list = SPLIT(reference3,C_BYTE1)
	_correct_guesses = reference4
	
	if !(reference0 == "start"); _word_list[0] = IARRAY
	if _word_list[0] == ""
	{
		"\C\![raise,OnWordJumble.Win,,,""%(_category_name)"",""%(_word_list)"",""%(_correct_guesses)"",""%(_scrambled)""]"
		return
	}
	
	_word = _word_list[0]
	_scrambled = WordJumble.Scramble(_word)
	
	_word_list = WordJumble.MakeMultiArray(,_word_list)
	
	"\C\![lock,balloonrepaint]\c"
	"\0\b[2]\_q\*\t"
	"\![no-markdown]" //I'm silly and testing this in a ghost with markdown. But maybe I'll leave it? Shouldn't hurt
	"\![open,inputbox,OnWordJumble.Guess,--reference=""%(_category_name)"",--reference=""%(_word_list)"",--reference=""%(_correct_guesses)"",--reference=""%(_scrambled)""]"
	
	"Unscramble the words!"
	"\n"
	"Category: %(_category_name)"
	"\n\n"
	"\![*]\__q[OnWordJumble.Quit,,,""%(_category_name)"",""%(_word_list)"",""%(_correct_guesses)"",""%(_scrambled)""]Give up\__q"
	"\n\n"
	
	if _correct_guesses != ""; WordJumble.MakeDisplay(_correct_guesses)
	
	"%(_scrambled)"
	"\![unlock,balloonrepaint]"
}

OnWordJumble.Guess
{
	_category_name = reference2
	_word_list = reference3
	_word = SPLIT(_word_list,C_BYTE1)[0]
	_word = SHIORI3FW.EscapeAllTags(_word)
	_correct_guesses = reference4
	_scrambled = reference5
	
	if reference0 == ""; "\C\![open,inputbox,OnWordJumble.Guess,--reference=""%(_category_name)"",--reference=""%(_word_list)"",--reference=""%(_correct_guesses)"",--reference=""%(_scrambled)""]"
	elseif TOLOWER(reference0) == TOLOWER(_word) //Correct guess, game continues
	{
		if _correct_guesses != ""; _correct_guesses += WordJumble.Delimiter
		_correct_guesses += "%(_word)%(C_BYTE1)%(_scrambled)"
		
		"\C\![raise,OnWordJumble.Display,,,""%(_category_name)"",""%(_word_list)"",""%(_correct_guesses)"",""%(_scrambled)""]"
	}
	else //Lose
	{	
		"\C\![raise,OnWordJumble.Lose,""%(reference0)"",,""%(_category_name)"",""%(_word_list)"",""%(_correct_guesses)"",""%(_scrambled)""]"
	}
}


//——————————————— Utility functions ———————————————

//If for some reason the double pipe doesn't work as a delimiter for you, you can pick something else here
WordJumble.Delimiter
{
	"||"
}

//Scrambles whatever word is input
WordJumble.Scramble
{
	_word = TOLOWER(_argv[0])
	_output = ""
	//Try up to 100 times to scramble the word without just outputting the original word again.
	//There is a chance someone puts in a word that can't be scrambled, like if they just put "aaa". So this stops after 100 tries so as not to potentially crash SSP
	for _i = 0; _i < 100; _i++
	{
		_output = ""
		while STRLEN(_word) > 0
		{
			_n = RAND(STRLEN(_word))
			_output += SUBSTR(_word,_n,1)
			_word = ERASE(_word,_n,1)
		}
		
		if _output != TOLOWER(_argv[0]); break
	}
	_output
}

//Make a general purpose array into a simple array
//The first argument is the delimiter. YOU MUST INCLUDE THIS ARGUMENT.
//If you want to use C_BYTE1, you may leave the argument blank. But it MUST exist, even if it's empty.
//All other arguments are elements of the array that will be made into a simple array
WordJumble.MakeMultiArray
{
	//I hate this
	_delimiter = C_BYTE1
	if _argv[0] != ""; _delimiter = _argv[0]
	_argv[0] = IARRAY
	
	_output = ""
	foreach _argv; _arg
	{
		if _output != ""; _output += _delimiter
		_output += _arg
	}
	_output
}

WordJumble.MakeDisplay : all
{
	_correct_guesses = _argv[0]
	_correct_guesses = SPLIT(_correct_guesses,WordJumble.Delimiter)
	foreach _correct_guesses; _guess
	{
		_w = _guess[0,C_BYTE1]
		_w = SHIORI3FW.EscapeAllTags(_w)
		_s = _guess[1,C_BYTE1]
		"\f[color,disable]%(_s)\n"
		"\f[color,default.anchor]%(_w)\f[color,default]\n\n"
	}
}

//Hangman
//Written by Zichqec https://ukagaka.zichqec.com/index.html

OnStartHangman //Sets up starting variables
{
	HMword = hangmanwords //Pulls the word to guess from a pool of words
	HMlet = IARRAY //HMlet[0] is guessed letters, HMlet[1] is incorrect letters
	OnHangman
}

OnHangman //Main game window
{
	_maxwrong = 6 //Maximum incorrect guesses - set to whatever you like
	_word = ""
	_left = 0
	for _i = 0; _i < STRLEN(HMword); _i++
	{
		_let = TOLOWER(SUBSTR(HMword,_i,1)) //Get the current letter and make it lowercase
		if _let _in_ HMlet[0] || _let == " "
		{
			_word += "%(SUBSTR(HMword,_i,1))"
		}
		else
		{
			_word += "_ "
			_left++
		}
	}
	--
	if (STRLEN(HMlet[1]) / 2) >= _maxwrong //If the number of incorrect letters is equal to or greater than the number of max tries
	{
		"\0\s[0]Nope, you're all out of guesses! The word was %(HMword). Better luck next time!"
	}
	elseif _left > 0 //If there are still letters to guess
	{
		_mistakes = (_maxwrong - (STRLEN(HMlet[1]) / 2))
		_marker = ""
		
		"\0\_q\*%(bb)\![set,balloontimeout,-1]\![set,autoscroll,disable]\![open,inputbox,OnGuessNormal,-1]"
		--
		"%(_word)\n\nGuessed so far: %(HMlet[1])\n\nTries left: "
		--
		for _i = 0; _i < _mistakes; _i++
		{
			_marker += "\![*]"
		}
		--
		"%(_marker)"
		--
		if _left > 1
		{
			"\n\n\![*]\q[Guess the whole word,HangmanGuessWhole]"
		}
		--
		"\n\![*]\q[Open the input box again,HangmanInput]\n\n\![*]\q[Give up,HangmanQuit]\e"
	}
	else //If _left is still 0, the user has guessed all letters correctly
	{
		HangmanWin
		--
		"\1\s[10]\0\s[1]\![embed,OnWoohooSoundBite]Woohoo! It was %(HMword)."
	}
	--
	HangmanBack
}

OnGuessNormal
{
	if TOSTR(reference0) == "0.0" || TOSTR(reference0) == "0.000000"; reference0 = "." //Unlikely to come up, but periods are handled strangely, and also differently between aya and yaya because screw you lol
	if STRLEN(reference0) > 1 //The user has put in more than 1 letter
	{
		"\0\s[0]For some reason, according to Lisa who read the rules while I was asleep, you can only guess one letter at a time.\x"
	}
	elseif TOLOWER(reference0) _in_ TOLOWER(HMlet[0]) //The user has already guessed that letter
	{
		"\0\s[0]You already guessed %(reference0)! Don't worry, I also almost forgot, myself.\x"
	}
	elseif TOLOWER(reference0) _in_ TOLOWER(HMword) //The letter is in the word
	{
		HMlet[0] += reference0
	}
	else //The letter is not in the word
	{
		HMlet[0] += reference0
		HMlet[1] += "%(TOLOWER(reference0)) "
	}
	--
	OnHangman
}

Select.HangmanGuessWhole
{
	_word = ""
	_left = 0
	for _i = 0; _i < STRLEN(HMword); _i++
	{
		_let = TOLOWER(SUBSTR(HMword,_i,1)) //Get the current letter and make it lowercase
		if _let _in_ HMlet[0] || _let == " "
		{
			_word += "%(SUBSTR(HMword,_i,1))"
		}
		else
		{
			_word += "_ "
			_left++
		}
	}
	"\0\*%(b)\![set,balloontimeout,-1]\![set,autoscroll,disable]\![close,inputbox,OnGuessNormal]\![open,inputbox,OnGuessWhole,-1]"
	--
	"\0\s[0]Ooh, trying to guess the whole word at once? Okay, the word so far is %(_word)\n\nYou've guessed these letters so far: %(HMlet[1])\n\nWhat do you think the word is?"
	--
	"\n\n\n\![*]\q[Open the input box again,WholeInput]\n\n\![*]\q[Give up,HangmanQuit]\e"
}

OnGuessWhole
{
	_word = ""
	_left = 0
	for _i = 0; _i < STRLEN(HMword); _i++
	{
		_let = TOLOWER(SUBSTR(HMword,_i,1)) //Get the current letter and make it lowercase
		if _let _in_ HMlet[0] || _let == " "
		{
			_word += "%(SUBSTR(HMword,_i,1))"
		}
		else
		{
			_word += "_ "
			_left++
		}
	}
	if TOLOWER(reference0) == TOLOWER(HMword) //Correct guess, instant win
	{
		HangmanWin
		--
		"\1\s[10]\0\s[1]\![embed,OnWoohooSoundBite]Woohoo! It was %(HMword)! You guessed it with %(_left) letters still blank! Wow!"
	}
	else //Incorrect guess, instant lose
	{
		"\1\s[10]\0\s[3]Nope, it was %(HMword)! This is why I don't usually try and guess the whole word. It usually seems to trip me up!"
	}
	--
	HangmanBack
}

Select.HangmanInput
{
	"\C\![close,inputbox,OnGuessNormal]\![open,inputbox,OnGuessNormal,-1]\_l[0,0] "
}

Select.WholeInput
{
	"\C\![close,inputbox,OnGuessWhole]\![open,inputbox,OnGuessWhole,-1]\_l[0,0] "
}

Select.HangmanQuit
{
	"\0\![close,inputbox,OnGuessNormal]\![close,inputbox,OnGuessWhole]%(b)"
	--
	"\1\s[10]\0\s[3]Aw, OK then. It was %(HMword). This game isn't my favorite anyway..."
	--
	HangmanBack
}

HangmanBack
{
	"\n\n\![*]\q[Another!,OnStartHangman]\n\![*]\q[I'm done,OnRestoreDonut]\e"
}

HangmanWin //This function is called every time the user wins hangman. You can use it to easily add friendship/money/whatever in one easy place. This does not include dialogue, it's just for variable changes. The ones I've left in here are examples.
{
	//friendship++
	//money++
}

hangmanwords : nonoverlap //Words it can choose - add lots of these!
{
	"Brownie"
	"Cake"
	"Cupcake"
	"Donut"
	"Chocolate"
	"Candy"
	"Cookie"
	"Cream"
	"Beer"
	"Chicken"
	"Porkchop"
	"Honey"
	"Sandwich"
	"Lollipop"
	"Simpsons"
	"Homer"
	"Marge"
	"Lisa"
	"Bart"
	"Maggie"
	"Duff"
	"Abraham"
	"Mona"
	"Yummy"
	"Delicious"
	"Lobster"
	"Pinchy"
	"Crayon"
	"Plopper"
	"Tulip"
	"Peanut"
	"Kavner"
	"Barney"
	"Quimby"
	"Yeardley"
	"Springfield"
	"Waylon"
	"Smithers"
	"Burns"
	"Flanders"
	"Seymour"
	"Skinner"
	"Milhouse"
}
OnRestoreDonut
{
"\1\s[10]\0"
}
